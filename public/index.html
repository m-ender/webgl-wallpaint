<!DOCTYPE html>
<html>
<head>
    <meta charset=utf-8 />
    <title>WebGL Wall Paint</title>

    <link rel="stylesheet" type="text/css" href="main.css" />

    <script type="text/javascript" src="jquery-2.1.0.js"></script>
    <script type="text/javascript" src="jquery.color-2.1.2.js"></script>

    <script type="text/javascript" src="webgl-utils.js"></script>

    <script type="text/javascript" src="math.js"></script>
    <script type="text/javascript" src="Configuration.js"></script>
    <script type="text/javascript" src="ConvexPolygon.js"></script>
    <script type="text/javascript" src="main.js"></script>

    <!--[if IE]>
        <script src="http://html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <script id="2d-vertex-shader" type="x-shader/x-vertex">
        uniform vec4 uColor;

        attribute vec4 aPos;

        varying vec4 vColor;
        varying vec4 vPos;

        void main()
        {
            vColor = uColor;

            // The render scale shrinks or grows the viewport and refers to which percentage
            // of the screen will be filled by the [-1, 1] coordinate range.
            vPos = vec4(aPos.x,
                        aPos.y,
                        0,
                        aPos.w);
            gl_Position = vPos;
        }
    </script>

    <script id="minimal-fragment-shader" type="x-shader/x-fragment">
        #ifdef GL_ES
        precision mediump float;
        #endif
        #extension GL_OES_standard_derivatives : enable

        uniform float uSeed;
        uniform vec2 uLightPos;

        varying vec4 vColor;
        varying vec4 vPos;

        //
        // Description : Array and textureless GLSL 2D simplex noise function.
        //      Author : Ian McEwan, Ashima Arts.
        //  Maintainer : ijm
        //     Lastmod : 20110822 (ijm)
        //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
        //               Distributed under the MIT License. See LICENSE file.
        //               https://github.com/ashima/webgl-noise
        //

        vec3 mod289(vec3 x) {
          return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec2 mod289(vec2 x) {
          return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec3 permute(vec3 x) {
          return mod289(((x*34.0)+1.0)*x);
        }

        float snoise(vec2 v)
          {
          const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                              0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                             -0.577350269189626,  // -1.0 + 2.0 * C.x
                              0.024390243902439); // 1.0 / 41.0
        // First corner
          vec2 i  = floor(v + dot(v, C.yy) );
          vec2 x0 = v -   i + dot(i, C.xx);

        // Other corners
          vec2 i1;
          //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
          //i1.y = 1.0 - i1.x;
          i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
          // x0 = x0 - 0.0 + 0.0 * C.xx ;
          // x1 = x0 - i1 + 1.0 * C.xx ;
          // x2 = x0 - 1.0 + 2.0 * C.xx ;
          vec4 x12 = x0.xyxy + C.xxzz;
          x12.xy -= i1;

        // Permutations
          i = mod289(i); // Avoid truncation effects in permutation
          vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
                + i.x + vec3(0.0, i1.x, 1.0 ));

          vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
          m = m*m ;
          m = m*m ;

        // Gradients: 41 points uniformly over a line, mapped onto a diamond.
        // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

          vec3 x = 2.0 * fract(p * C.www) - 1.0;
          vec3 h = abs(x) - 0.5;
          vec3 ox = floor(x + 0.5);
          vec3 a0 = x - ox;

        // Normalise gradients implicitly by scaling m
        // Approximation of: m *= inversesqrt( a0*a0 + h*h );
          m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

        // Compute final noise value at P
          vec3 g;
          g.x  = a0.x  * x0.x  + h.x  * x0.y;
          g.yz = a0.yz * x12.xz + h.yz * x12.yw;
          return 130.0 * dot(m, g);
        }

        float tanh(float x)
        {
            return (exp(x)-exp(-x))/(exp(x)+exp(-x));
        }

        void main() {
            float seed = uSeed * 1.61803398875;
            // Choose a random light position
            vec3 light = vec3(uLightPos, 2.5);
            float x = vPos.x;
            float y = vPos.y;

            // Add a handful of octaves of simplex noise
            float noise = 0.0;
            for ( int i=4; i>0; i-- )
            {
                float oct = pow(2.0,float(i));
                noise += snoise(vec2(mod(seed,13.0)+x*oct,mod(seed*seed,11.0)+y*oct))/oct*4.0;
            }
            // Level off the noise with tanh
            noise = tanh(noise*noise)*2.0;
            // Add two smaller octaves to the top for extra graininess
            noise += sqrt(abs(noise))*snoise(vec2(mod(seed,13.0)+x*32.0,mod(seed*seed,11.0)+y*32.0))/32.0*3.0;
            noise += sqrt(abs(noise))*snoise(vec2(mod(seed,13.0)+x*64.0,mod(seed*seed,11.0)+y*64.0))/64.0*3.0;

            // And now, the lighting
            float dhdx = dFdx(noise);
            float dhdy = dFdy(noise);
            vec3 N = normalize(vec3(-dhdx, -dhdy, 1.0)); // surface normal
            vec3 L = normalize(light - vec3(vPos.x, vPos.y, 0.0)); // direction towards light source
            vec3 V = vec3(0.0, 0.0, 1.0); // direction towards viewpoint (straight up)
            float Rs = dot(2.0*N*dot(N,L) - L, V); // reflection coefficient of specular light, this is actually the dot product of V and and the direction of reflected light
            float k = 1.0; // specular exponent

            vec4 specularColor = vec4(0.4*pow(Rs,k));
            vec4 diffuseColor = vec4(0.508/4.0, 0.457/4.0, 0.417/4.0, 1.0)*dot(N,L);
            vec4 ambientColor = vec4(0.414/3.0, 0.379/3.0, 0.344/3.0, 1.0);

            gl_FragColor = specularColor + diffuseColor + ambientColor;
            gl_FragColor.a = 1.0;
        }
    </script>

</head>
<body id="body">
    <div id="title">WebGL Wall Paint</div>
    <canvas id="gl-canvas">
        Oops ... your browser doesn't support HTML5's Canvas elements!
    </canvas>
    <div id="menu">
        <span id="message"></span>
        <hr>
        <span id="options"></span>
    </div>
    <div id="debug">
        Normalised cursor coordinates:<br>
        x = <span id="xcoord"></span><br>
        y = <span id="ycoord"></span><br>
        <br>
        Last mouse down:<br>
        x = <span id="xdown"></span><br>
        y = <span id="ydown"></span><br>
        <br>
        Last mouse up:<br>
        x = <span id="xup"></span><br>
        y = <span id="yup"></span>
    </div>
</body>
</html>
